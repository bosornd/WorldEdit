buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        jcenter()
    }

    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'

configurations.all {
	resolutionStrategy {
		force 'org.ow2.asm:asm:5.2'
	}
}

def minecraftVersion = "1.13"
def forgeVersion = "24.0.16-1.13-pre"

dependencies {
    compile project(':worldedit-core')

    minecraft "net.minecraftforge.test:forge:${minecraftVersion}-${forgeVersion}"

    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.0-rc1'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    mappings channel: 'snapshot', version: '20181117'
//    runDir = 'run'

//    replaceIn "com/sk89q/worldedit/forge/ForgeWorldEdit.java"
//    replace "%VERSION%", project.version
}

project.archivesBaseName = "${project.archivesBaseName}-mc${minecraftVersion}"

processResources {
    from (sourceSets.main.resources.srcDirs) {
        expand 'version': project.version,
                'mcVersion': minecraftVersion,
                'forgeVersion': forgeVersion,
                'internalVersion': project.internalVersion
        include 'mcmod.info'
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes("Class-Path": "truezip.jar WorldEdit/truezip.jar js.jar WorldEdit/js.jar",
                   "WorldEdit-Version": version,
                   "FMLAT": "worldedit_at.cfg")
    }
}

shadowJar {
    dependencies {
        include(dependency(':worldedit-core'))
    }
}

//reobf {
//    shadowJar {
//        mappingType = 'SEARGE'
//    }
//}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

artifacts {
    archives deobfJar
}
